# naive native language processing for english based on my own loose skimming
# of random internet articles on grammar
main = {<sentence>};

#sentence := <clauses> ("." | "?" | "!");
sentence := declarative-sentence
          | interrogative-sentence;

declarative-sentence := clauses ("." | "!");
interrogative-sentence := interrogative-verb-phrase clauses "?";

clauses := (subclause {[","] conjunction clauses})
		 | subclause;

subclause = independent-clause
          | dependent-clause 
          | (clause {dependent-clause});

independent-clause := (clause object {dependent-clause})
                    | (clause object);

# XXX: what are the rules for prepositions here?
#dependent-clause := (preposition {clauses})
#                  | (preposition object)
#
#                  | (preposition noun)
#                  | (preposition verb noun)
#                  | (preposition verb)
#                  ;

dependent-clause := preposition
                    (
                     object
                     | (verb noun)
                     | verb
					 | ({clauses})
					 );

clause *:= subject verb;

#subject := pronoun | noun;
#object := pronoun | noun;

subject = noun;
object = [adjective] noun;

article *:= "an" | "a" | "the" | "that" | "those";

# XXX: can't handle un-capitalized nouns without a preceding article
# XXX: also consider quotations to be nouns for parsing purposes
atomic-noun := ("n:" {lowercase})
      | (referrent-pronoun common-noun)
      | pronoun
      | (article [adjective] common-noun)
      | proper-noun
      | quotation;
	
noun := noun-list | atomic-noun;

noun-list := (atomic-noun [<",">] noun-list)
           | ("and" atomic-noun);

common-noun *:= {lowercase};
proper-noun *:= (uppercase {lowercase});
# assume all pronouns are lowercase.
pronoun *:= "it" | "I"
          | "they" | "them" | "their" | "theirs"
          | "she" | "her" | "hers"
          | "he"  | "him" | "his"
          | "you" | "your" | "yours"
          | "that" ;

# TODO: what's the proper name for this?
referrent-pronoun *:= "your" | "their" | "his" | "her" | "my" | "this";

verb *:= ("v:" {lowercase})
       | (verb-phrase)
       | (linking-verb [verb])
       | (adverb verb)
       | (regular-verb {adverb})
       | (regular-verb);

linking-verb *:= ("am" | "is" | "was" | "are" | "were"
               | "has been" | "have been" | "must have been"
               | "might have been" | "must have" | "must" | "become"
               | "does" | "do" | "will" | "would" | "can" | "should") [negation];

verb-phrase *:= "look around";

negation *:= "not" | "n't";

# XXX: treat "who" as an interrogative adverb
interrogative-adverb *:= "what" | "where" | "how" | "when" | "why" | "who";

regular-verb *:= {lowercase};

# XXX: also assume all adjectives end in "ing" lol
adjective *:= ["very"] ({lowercase} "ing") | ("adj:" {lowercase});
# XXX: and "ly" for adverbs
#adverb *:= "very" | ({![" ly"]} "ly");
adverb *:= ["very"] ("adv:" {lowercase});

# XXX: needed to prevent starting a question with any verb
interrogative-verb-phrase *:= [interrogative-adverb] linking-verb;

# subset of https://www.englishclub.com/grammar/prepositions-list.htm
preposition *:= "as" | "at" | "and" | "but" | "by" | "for" | "from"
              | "inside" | "into" | "in" | "like" | "near" | "of" | "off"
              | "onto" | "on" | "over" | "per" | "than" | "to"
              | "under" | "unlike" | "until" | "up" | "versus"
              | "via" | "with" | "within" | "without";

conjunction := coordinating_conjunct
             | subordinating_conjunct
             | correlating_conjuct;

coordinating_conjunct *:= "for" | "and" | "nor" | "but" | "or" | "yet" | "so";
subordinating_conjunct *:=
    "after" | "although" | "as" | "as if" | "as long as"
    | "as much as" | "as soon as" | "as though" | "because" | "before"
    | "by the time" | "even if" | "even though" | "if" | "in order that"
    | "in case" | "lest" | "once" | "only if" | "provided that" | "since"
    | "so that" | "than" | "that" | "though" | "till"
    | "unless" | "until" | "when" | "whenever" | "where" | "wherever" | "while"
	| "then";

correlating_conjuct *:= "asdf";

quotation *:= "\"" {![\"]} "\"";
